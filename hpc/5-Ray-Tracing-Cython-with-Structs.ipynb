{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "import numpy as np\n",
    "import matplotlib.pyplot as plt\n",
    "%matplotlib inline\n",
    "import cython\n",
    "%load_ext Cython"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We now use a pure C structure to represent a 3D vector. We also implement all operations we need by hand in pure C."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython\n",
    "cimport cython\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "DBL = np.double\n",
    "ctypedef np.double_t DBL_C\n",
    "from libc.math cimport sqrt\n",
    "\n",
    "cdef int w, h\n",
    "\n",
    "cdef struct Vec3:\n",
    "    double x, y, z\n",
    "    \n",
    "cdef Vec3 vec3(double x, double y, double z):\n",
    "    cdef Vec3 v\n",
    "    v.x = x\n",
    "    v.y = y\n",
    "    v.z = z\n",
    "    return v\n",
    "\n",
    "cdef double dot(Vec3 x, Vec3 y):\n",
    "    return x.x * y.x + x.y * y.y + x.z * y.z\n",
    "\n",
    "cdef Vec3 normalize(Vec3 x):\n",
    "    cdef double n\n",
    "    n = sqrt(x.x * x.x + x.y * x.y + x.z * x.z)\n",
    "    return vec3(x.x/n,  x.y/n, x.z/n)\n",
    "\n",
    "cdef double max(double x, double y):\n",
    "    return x if x > y else y\n",
    "\n",
    "cdef double min(double x, double y):\n",
    "    return x if x < y else y\n",
    "\n",
    "cdef double clip_(double x, double m, double M):\n",
    "    return min(max(x, m), M)\n",
    "\n",
    "cdef Vec3 clip(Vec3 x, double m, double M):\n",
    "    return vec3(clip_(x.x, m, M), clip_(x.y, m, M), clip_(x.z, m, M),)\n",
    "\n",
    "cdef Vec3 add(Vec3 x, Vec3 y):\n",
    "    return vec3(x.x + y.x, x.y + y.y, x.z + y.z)\n",
    "\n",
    "cdef Vec3 subtract(Vec3 x, Vec3 y):\n",
    "    return vec3(x.x - y.x, x.y - y.y, x.z - y.z)\n",
    "\n",
    "cdef Vec3 minus(Vec3 x):\n",
    "    return vec3(-x.x, -x.y, -x.z)\n",
    "\n",
    "cdef Vec3 multiply(Vec3 x, Vec3 y):\n",
    "    return vec3(x.x * y.x, x.y * y.y, x.z * y.z)\n",
    "    \n",
    "cdef Vec3 multiply_s(Vec3 x, double c):\n",
    "    return vec3(x.x * c, x.y * c, x.z * c)\n",
    "\n",
    "cdef double intersect_sphere(Vec3 O, \n",
    "                      Vec3 D, \n",
    "                      Vec3 S, \n",
    "                      double R):\n",
    "    # Return the distance from O to the intersection of the ray (O, D) with the \n",
    "    # sphere (S, R), or +inf if there is no intersection.\n",
    "    # O and S are 3D points, D (direction) is a normalized vector, R is a scalar.\n",
    "    cdef double a, b, c, disc, distSqrt, q, t0, t1\n",
    "    cdef Vec3 OS\n",
    "    \n",
    "    a = dot(D, D)\n",
    "    OS = subtract(O, S)\n",
    "    b = 2 * dot(D, OS)\n",
    "    c = dot(OS, OS) - R * R\n",
    "    disc = b * b - 4 * a * c\n",
    "    if disc > 0:\n",
    "        distSqrt = sqrt(disc)\n",
    "        q = (-b - distSqrt) / 2.0 if b < 0 else (-b + distSqrt) / 2.0\n",
    "        t0 = q / a\n",
    "        t1 = c / q\n",
    "        t0, t1 = min(t0, t1), max(t0, t1)\n",
    "        if t1 >= 0:\n",
    "            return t1 if t0 < 0 else t0\n",
    "    return 1000000\n",
    "\n",
    "cdef Vec3 trace_ray(Vec3 O, Vec3 D,):\n",
    "    \n",
    "    cdef double t, radius, diffuse, specular_k, specular_c, DF, SP\n",
    "    cdef Vec3 M, N, L, toL, toO, col_ray, \\\n",
    "        position, color, color_light, ambient\n",
    "\n",
    "    # Sphere properties.\n",
    "    position = vec3(0., 0., 1.)\n",
    "    radius = 1.\n",
    "    color = vec3(0., 0., 1.)\n",
    "    diffuse = 1.\n",
    "    specular_c = 1.\n",
    "    specular_k = 50.\n",
    "    \n",
    "    # Light position and color.\n",
    "    L = vec3(5., 5., -10.)\n",
    "    color_light = vec3(1., 1., 1.)\n",
    "    ambient = vec3(.05, .05, .05)\n",
    "    \n",
    "    # Find first point of intersection with the scene.\n",
    "    t = intersect_sphere(O, D, position, radius)\n",
    "    # Return None if the ray does not intersect any object.\n",
    "    if t == 1000000:\n",
    "        col_ray.x = 1000000\n",
    "        return col_ray\n",
    "    # Find the point of intersection on the object.\n",
    "    M = vec3(O.x + D.x * t, O.y + D.y * t, O.z + D.z * t)\n",
    "    N = normalize(subtract(M, position))\n",
    "    toL = normalize(subtract(L, M))\n",
    "    toO = normalize(subtract(O, M))\n",
    "    DF = diffuse * max(dot(N, toL), 0)\n",
    "    SP = specular_c * max(dot(N, normalize(add(toL, toO))), 0) ** specular_k\n",
    "    \n",
    "    return add(ambient, add(multiply_s(color, DF), multiply_s(color_light, SP)))\n",
    "\n",
    "def run(int w, int h):\n",
    "    cdef DBL_C[:,:,:] img = np.zeros((h, w, 3))\n",
    "    cdef Vec3 img_\n",
    "    cdef int i, j\n",
    "    cdef double x, y\n",
    "    cdef Vec3 O, Q, D, col_ray\n",
    "    cdef double w_ = float(w)\n",
    "    cdef double h_ = float(h)\n",
    "    \n",
    "    col_ray = vec3(0., 0., 0.)\n",
    "    \n",
    "    # Camera.\n",
    "    O = vec3(0., 0., -1.)  # Position.\n",
    "        \n",
    "    # Loop through all pixels.\n",
    "    for i in range(w):\n",
    "        Q = vec3(0., 0., 0.)\n",
    "        for j in range(h):\n",
    "            x = -1. + 2*(i)/w_\n",
    "            y = -1. + 2*(j)/h_\n",
    "            Q.x = x\n",
    "            Q.y = y\n",
    "            col_ray = trace_ray(O, normalize(subtract(Q, O)))\n",
    "            if col_ray.x == 1000000:\n",
    "                continue\n",
    "            img_ = clip(col_ray, 0., 1.)\n",
    "            img[h - j - 1, i, 0] = img_.x\n",
    "            img[h - j - 1, i, 1] = img_.y\n",
    "            img[h - j - 1, i, 2] = img_.z\n",
    "    return img"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "image/png": "iVBORw0KGgoAAAANSUhEUgAAAOsAAADrCAYAAACICmHVAAAABHNCSVQICAgIfAhkiAAAAAlwSFlzAAALEgAACxIB0t1+/AAAADh0RVh0U29mdHdhcmUAbWF0cGxvdGxpYiB2ZXJzaW9uMy4xLjIsIGh0dHA6Ly9tYXRwbG90bGliLm9yZy8li6FKAAAeYElEQVR4nO2dO8ss21aG36r+1rdutc/ZRwz0wMFEMNB/YGJgoBiqgRqKgYiIIoIiehRREFFExEAM1UANRQMDE/+BBoKJCGpwwHOpvb5rVxnMHj1HjRpz1qzuXmuv2fU+UNSlZ1d3dfdTY8xLVTfjOIIQ8vHTft5vgBBSBmUlpBIoKyGVQFkJqQTKSkglUFZCKuFmTeGmadjPQ8h7ZhzHxtvOyEpIJVBWQiqBshJSCZSVkEqgrIRUAmUlpBIoKyGVQFkJqQTKSkglUFZCKoGyElIJlJWQSqCshFQCZSWkEigrIZVAWQmpBMpKSCVQVkIqgbISUgmUlZBKoKyEVAJlJaQSKCshlUBZCakEykpIJVBWQiqBshJSCZSVkEqgrIRUAmUlpBIoKyGVQFkJqQTKSkglUFZCKoGyElIJlJWQSqCshFQCZSWkEigrIZVAWQmpBMpKSCVQVkIqgbISUgmUlZBKuPm83wA5ja77BwCvALwAsDtsvUE4/+4O8xZAo+YyWQYAz+j773vP75qcQzOOY3nhpikvTC5G1/0tgLcAbhHkvEEQUk9AFFRPnqwirJ2Par4H8IC+/573ckwkzTiO3hmVsn6MdN3fHZZeI8gpgmpJWzMH5oKWyGqF1YyHaTisP6Pvv3L28ZE8lPUjp+v+GkHO28METAXVKa6VVZoecqJ6qXAq0upl/ZWLuAOAJ/T9l884YpKCsn6kBElfIaa4tg5q010raamsdhnIR1pgHm11miyp8h59/+2nHDpJQFk/IrruzwB8AVNJbzCNpIAvqierfLepNLgkspamxsKIaZr8DOAeff+dKz4J4kFZPwK67o8BfIoo6S2mkupICvh1U68RSSKrl/aWRtacrClpdaSV9PgZwB0AoO+/Y/EzIXMo6+dI1/35Yek1gJeYR9Md5ikvUCaqSAiUR9WUrEutxSnkZzEgpsdAaJD6wsJziYWyfg503Z8gdLm8OmzR9dKUqLp11wrq9Z+uiax6HVgWGJiKWiKtTECQdw/gEX3/xYXnEiElK0cwEVIJjKzvga77XQBfwrwrRkdVL/XV0RVYrqfaNLgkBc5F1lR0PRVbl33HluMCGFk/EF33BwC+DVNRraS2ddeTz0q3ZsKFHjuXBvGkdAvgE3Tdty6w321CWS9I1/0hgA7zLpk1XTCnCrpGzhSXFNXud4fwebxC191deP/bgAP5L0DX/QZCl8wbhNZeb3ggkJbTExUoFxFm2T4fZr0kwr4PGshIrK57AHCHvv/0Pb7edcHIeiZd91UEUSXtLU13z0lzS9NZmHKWDyWppUX4nN6i6/oP+Lp1w8h6BkHUTzCNpilJAV/SlLDIbMfCcmlktXxIYSXKNui6zwAAff/2A75+fVDWE+i63zwspS5by40wWpJ0qbEnFT0ty5G1OcHNFZ0HhewQTnZA192h719f+gWuBqbBK+m6X0eom75BWSPSmnQ2Ryqylm1rGpma43RKGhz3U1S8EPnMbtn4lIGyrqDrfhVR0lxEtZHTExeJ5aVGn5ScXuRsjFiXTXMvL24LCpuGshbSdb+CKKpt6U2JuhRdgZRoc1LCzsvNBfJTaR1ltXhLk/uqF5OWwqagrAV03S8itvbaK2RKWniF0saflMDLNsyjaKMea0y0PU3UnLiXibahtVgankiAshJSCWwNXqDrfg7z9FfOcd4gh1w9M9UAlApDZenxUp1UomhYnkZ3GwH1+lLL7zjG8l7Zpjmn9Ti0Enddj77vTt3JVUFZM3TdTyMMH5TGpNwNymyjUom4a+uq8+1+uqnFDOsiqZZVp7XHZ5r9WdlkXURNPW73d5q0IuzXOdIJlHWBL2Aqqr0qJtdgVBJhzyPX2GMjqe6uEYltHdTuNyeqTLnoqbefHmV3CO0FhLImCOmvjEzyWnyBfGuv5jJyTvaYFFWLaFt8W+jUt7SlV0umRfUmXd4KepqwDYAbDpgAZXUJ6a++273XNQMsR9HTWnSXmEtlI2Uzi6ht2876Xdu2TFgvog7DfJ7ifGFbBGG/hb7/ZO2TrwbKaui6H0doUNIDHrxGJCTmqdTXLpcyTp6X6i6R/U8jaYimIqrI2R7ONW2LyTbbPWMjpUipBZWpaeJ2+zy7fJqwMsLpa5u9gJ2yzvgUvqi2EUn4kKnvVNywbbqshxQGSYOsIuZuN5d1t/OFBebyiZz7/Xwuwqai7HnCNgjfx5s1T7oqKKui634K03qqN9hBU9qieypRzlxEtemviBqnIKSeAEy2i7girEbk06Lq6fk5iirS6vKTIzpLWEmHv7HJG7BR1gny3zJL/adeq+4lxZVfcGO22XRYt/LGqNq2MrUTQW9u4hyYrlthj6+qIqsVVKa2DfP9Xr+3SE7YdUhmc7tU8CqhrAC67icPS7cov2JGyNVRT8FLdf1663wYoJa0Rds2EyFfvIhzYLrNCqu7b0Q2EfXpKcj59BQnEbZpwtySEva06LpD130TADZ1X+LNy9p1P4R4X9+lBqVzWfpVzkX1t1lhw4qIGua7o4Qi5YsXwO1tlFWWZdLCerJKJBVBHx/DpCVPHlmmW2fdwAmJruGn23X/u5k7/3NsMCGVsPnICnwRoZ4KnJYCr8ULH3IX+3lUbRovJZbl8B5inTWkwbtde0x9Jare3obp5cswB8KyrOvoqqOkbiSSqCoR9eHBj8aTIzB9s+cNPzweOeKQz+0MlNi0rF33gwitv94/h6e6ZJDYliInZ1rS+evEsk0zmoEMkgZL6+9uJqqI+epVmIC4Lo+JsNKVo1t4gSCriHp/P5V7afSTHp7oPQ6sqb/q7+UGXfc/m/j3uk3LGsb+6r9YXLqjQ4mko5nb5aXn2u4aLem0tG75BTBrAbayvnoFvH4dJiAui8ASYUVAIMgjskpUvb+fl/Wu1tGDJto2PWBiPVJvxWH+KlP2eti4rC8xveQtJeql0H/aZLfpCKuFnw+CsFFVNzDtds0kBdZR9fVr4M2bMAFx+c2bKKxEVxFQR1ZJfbWsEoWBaboLTGVNpcPyPH18p/S9boFtHKVD1/0Ioqh2rK9dLsVG1dSvLpUCh222nhq2TfcV66tQkbU9RlbbAiwyvnkDvD3c8fPtW6DrpsJKOpyS9f4eePfOF1UPmgBiV4+OriLq6VfhHD8BNW83MQxxs7LGuqoemXRONF0Sc+m5qbrrPOLabptpPyuSabCkvSJr1wGffBLW376NKbEWcRhiv+njI3B3F/bnpcp6oAQQl/f7sD+dDsv7X7qAvQzd4HS9bFLWrvsBTKPq2miqI6eVMRdVbcOSJ6Pen5cCj2oZJrI2E1FTdVZJg7tuKqxEV5HRyvrwMI28QIykeoDE01N4TJZllJO9yid1Pey6qCvf2Q5d99/o+y+XPrE6NilraO7XUdVrSDonulqZl56Tq7NKCmy7bPw6ayqyWmGBmBKLtBJdc7JaUb3uHD1CSrp2drso7eVS4eMncpheLBWsmo3Kqsf/rhEz98vK1Ve9SOzXWb0ra7zX965BlTRYhNUjmERY3XWjG5y0rC9fxjR3GGKktKKKpA8PYZ+SIouskk7bYYyp62bPk1e3EF8nm5O1674fQVQvipamwKk0OBVZSxqaRNLU/iX9nUdZHVnlUjdPWhHpZfi3ill3zps3sd4qkXUco6xytY6IKi3DMrBCd+dIee+KHju2+fhpnNUqDISGpv9C339l7ROrYHOy+g1LJZSks97cllmqq85bh0PE9eursi5zK6q94kZaiIF5imwHTQg71XYj9VMtqJZUJnmeHt3kvefT8D5bOele70/6uvMGQq6I6z0NJXmBstR3bQuvV85Lk09tDfb36dX/bH3Wi7TANBLq1mOL7F+PH7aNRzblBfxbxqTqq+tJfSfXG3+u98gcuu57se7St5SIJV02S/XV1L5SQnv7mj4mY4aBuRy5yUqVwqbZti7qNSLl0t7TpU19puE77bp/P3XHHzUbi6wdyuqqnhQyt4LmhE49f32d1X9+yQnBHMmI2eB673aiqefaoYNrxvtqObXE6xqSlk6KDeT/Xq+NTUXWOBAiFVVTYgCYyZeSdk2UXCrrvbf0NI6DK5Ieo6sne4sWPaDBQ49QkpFJdvyvlXlyBKuk9Fg6Ocn26xzNtLHIKl02a8kJ7J3pbdkSeZuV5bWksXxKUCslMB3MoO/6AIS6qe26ub8P3TVSVo9YsvIC80H8k090tbhLJznNRSrFHx0bkzWVAq+JkvZ5JXJ5LAmb3l+QM8zDFOywkdWTVAQFplfR6AEP+32U1Y5gursL0/19lNeTFkhfdXP8BByJ/c/Jfv6pz+i62YysXffd8LN+K6q3LSdtidjeD8urr6ajswgqgyKCoMNB0FBeloehSaa4Dw9hAvxB+cMwHW44jnNZP/ssXHmjpdWRVsp7aXKZoKnvwH42trxsa9B1/4q+/76SF6qGzcg6v0A5J8fS47ntuehasr3Jlh9HLazUU0NkHYYBw7DHfn8zE9VeiwrMLzSXkUmpscFy1c27d0FYkdaLssA0NV6qz06/Fz1PfS9LJ8br+2lf3xElSdVXvV/NqfIh83hqv7aumovaMaqOY4sYWUPeOQz7w5SWVY9g0tejipQPD0FWeUxHVn09q5ZVhJX6rJZVhN3vp6L6wnonwrWfqWy7vrbTDcma6hJZkukUMU/ff4iccLZJ2eFwLAPGsZlE1nHcHyLrHvv9biKqHcQARFFFyMfH+R0gtKySRkt07fsw6ZRYoquUtxeg+/XW1Odpvy/7Gea+y+tjo7IK9odRKlepoKeeDOCW1/VWia7jOGAYYhq83+/Rts94ft5htwvC2BFGdvC8jPeVqKrvFGHvwSSptE6HRVapD0sDlkRW2zIdI+v0mIOzsux9N2u+o+vj+nIFQq6UDUbWc1LZzzeyxggbUuEQXRtVZ20P0x77/TOen2+Of23h/emUjqqPj/EuEPqOhTayPj3FBiXpxtFpsK6zev2vflRNpb52vuY7uz42JCvw4eS7tKzD5F5F0zprc0yDm2aPYWix3z8j/tVjOxunq28VqmW1N/qWsvoGaPqOECKsnts0WP+ZVayvyjHpAR2yXc/ttrXfwXWxIVk/pHznyRpvKDZ/PPy4bSNTsCmI22C/D7d6eX5u0DS3k5uq6VuFSouxbinWd9i35aUOqoUVaXV9NR9Zpb49mmPS35NdTn1+OHwWo5pT1itgrUwDQpW+pLzIcwlZ5XWB/PuQFuAQXYHQdSP3ZBJhw/u6PZRNy6ovfdOXuXnltbBaXD0MEZhG1iBqaAyLI688qeLJqkxWLaoV9rrYuKwD4hecKpMSxiuvpbUC595D65SD6saRCDSYq1RidA3lGwzDHvPb1QDAi4PYc/ns9an2X+Tk9fSoKHs3w9QIphhVRVQbVe13ZJdTwg5m3X6nib9fr5gNyarFTJ2FU+nU2khZWt6Kqtdh1ucnjNi/KpFVjnX+Vxuh/ItDnTZsEemW/p9VyyqDJ/Qkgnr3Dd7vRyWpjarymczf63w59ZnqqMrIeiXkvtTBlPGESkVAG0FtdAX8luilaA21HsuGaDttcBJZAWAYGgB7c+zjYczweBjdFL52iZK6H9ZehK5l9S6t06mx3hbK7w8RdTymv/r4pnVVeZ/T9fl86QTLyHoFDJnJS7FkspHP3hImlfZ6JwONFdVG0VBmmgpLJG2PAyPi+4joVFdaX/UVOvuDTfv9DXa73fGWL6lbhVpZtbB2Ct1GMkhjPMqqI2q6QQnOdu8kNpjl1HRdbFRWmwoPpsxSuurJnYquJWmzF7VxfEzk1NsCrWodVkc6YFLX1bIOw4C23R3KhWGJbRsmaTX2unlkv/paWV0flXRX5Azlragxms6jajyxmK2Jz8/WWfV3N2CeXdTPhmR9RvgCrbRLkXVNGnyJBibbGgyzPcwlHdbY379IE0Ud0bYD2lYi3x5NI7K2aJowhTpve3huM9m/yBeiZjwByHpMd6FExXE+T3V9cecnLW8C/BMwI2vlWFG9lGkpspakwWvqrEBZA5NE12mqLNtiv6wnLA4pcxxTLIMogqBB2CBqc5zrFmXZp5ZOhLUC6zR3Lmoqelo8UVOfn3dyHRBOztcFxwYTUgkbiqxPyEdXZB7LpcFeVPXqsJayBib9WqEFeBrpY8TVUTXWU8PtSeOkyw+DpL77Ywpso6qOrPIaNsLaSCvveToOeB7147ZT0uBUnXWvputiQ7I+YvpFerLqL97+CHJpsCeqToGX6qzAvD8VZl3qqsOhPjkVFoCS1nayzsWV8mFfIqkv63Evk3Q4TnIiieLCPGdeT9X7cx7JzPX3A/gn1z3Cyfm62JCsd5g2MllhAf+L1xI1sNHOb0zy6q0pyuqsS8ICmNRr42inEVp+3TAV9iHr0qjkiSojpJCc221eOc18SOGshDNfklWfjO8T+62Xzcja9z26TmT1hIVa9hotRFRZBvzICrOeaiBZiq6yXipseP8SVZtG0lfduNWquYg8KEkHJ7LGfej9pcWd9/la0hFV3qddzn1mfmTt+x/OvUiVbEbWwBNidC2NrKVpcC4Vhirv1WNTXTc20k4j7lRY2b+ty2ppR8S+WRxT3xBtpX6alhWYymi7dbztlryox1LOPCfr3kzX1xIMbE7WR0xl1dLisLw7zEUCL7qm0uClOmtKUC+a6jI2ugJTYRvz3DiwP44TbpREej4eH/Prq56c80g7XfeNzEl8KOEsL2UjXhp8ffVVYHOy3iPI6qXDQF5UGzUBP6qm+lqX8BqYUulwg2lKfHg3jX2uRM743kRcAE4UTUVVT04/wurteltZ/6ousxRV5XFbpXlGOClfHxuT9RHzVFhH1lwavCSr9xjM8lKUANZG1mkkDesxQtrU3dZjp9F0Ki/M8nTdF3QeVUO5otxXP0vNU5+XlwbLifizla9XB5uSNTQyfQnz6CqRVdJg+QHYCLskayoNLm0R1pLCrKcja0TqqO0hgmpRbcQF/Dpqmay+oNPGJz8ltpHXE3mNrLZK84S+/zFnn/WzKVkD9/CjK9Ryi3SddUlWmHmukSnV0OTVX/U+PXmlPBBbhUPZubi593+KrGEex/jqTAKZbUhsK0mB5TuzkfU666vAJmW9Q2xo0sJCLaeia4msXnSFKm+3WUFlGzBPf725FzWn22JD0xpZ5aSiy4/Oc0enLutF00vWV73IKt/nXcHr1AnHBhNSCZuLrKHe+iliKvyM+DHotFhSYXtPo5LICjXXy7m6a+s8XhpVbT8rME97W7ePc/pXHWWpcGw0stH0nKgq78HOUymwV2d9RN//aOFr1cfmZA3cAXiDKKt0okvXjZbV1l1FXqBM1pIGJsETtjGPeaLaMlNJA36ddd4CbKVNSWmPUc9hypeSk3XANA3WVRepq15nl42wUVkfDtNLAC8QZb2BL6uVUX4opbKuwQq71Mi0XGddbkDK1VW1cPZxb67f5/vqstGyysn2CddcXwU2KmtIhd8i/GfrC8SP4RnTgRGpQRI6rfUkAJZllR+3N3DCdt14kVO2W5mtyEsnlBJhYcp7x+cdeyklI5dsy7ycYEXWR/T9T6x83brYpKyBdwip8C2CsECUVSLqM+KP3/ux2wgLZ9374drU2Et/9XP98b++MLlUOIVXX/WE9VuD5+/h1Igqy1pYm/7qrhogRtXru8rGsllZ+/5b6LoOU1lvEGTVEVWnwrrBCZinxXrbWrznpFJgmZfWWVMZgF230nrHY0UGfGlLKY2qumFJD9bfRlQFNixr4B1CKnx7WNeytvBl1XKm0kk4j1vstly6vDT3ZFpKh1PvOxVdkVi37/1cWUujqsj6iC1EVWDzsspFylpWETYnq06DbWQVSn60+oeZSlWtEKl6KzAVc6neisT2VHQtmdvlJWzrryzn6qp2pNIjgG+ueM162bSsoaGpxVxWEXSHWG/1ZLVRNhddc+TqlCkhcttzMi49ntpPbp5b9vBSX1leagGWS+Ckm+Yeff+zC693HWxa1sCAmEbdYpoK7xAltY1MMPNTo2sOHXFrkjW3LVVHlXlKVjv+9+HwnK87r3GdbF7WEF1lTcuaSn9t5ISzfi7y49URt1GPtUgL8z5lBebHf4kU2Aqcq6tKVA19qn3/Cytes244NpiQSth8ZAVCdAWArpMRTV6LcK5OmopUS6QuF7PnUC8d1nMdiRu1zXvfS+83tQ617s3t4ylSrb+yPiCfAofW377/mYXXuT4o64QHhPTKaxFemy7msJLqOlpOVCutTYlT7+l9ymqXSzglBdbjf6/zThBLUFZFbB32+lvXRs0UqZbQVNk19VYbTaUbZulkg4LHgfnrnRpVvQYlvZ7qVw111b7/+YXXuU4o64wBYbCEdOGU/NiXomsq3dXLdgKmEVVETM3ltb301xtO6L3/nMgwy5dqWEoNhLCjlWRI4f+teK3rgrIaYuvwHZYj6zlpr15ORVibGueEhXqfuYh6ShoMs7wmsqaO3ZNVp8DT+yqFKkqPvv815zW2AWV1iMKmZC0hl+7m6mylkRWYRlKo5XPTYO8xmOU1kTV3jINZTqW/79D3v7zwOtcNZU0QhF2StUTeEUF6u03PZTlVh2sTyw3mkdUTdE0abI/rEinw0uAHuVghNQDiDn3/SwWvc91Q1ix7hJbHnKwpcqntzpRJ1VltK3Gu7irltcD6PS+NwForrF1OsVQ3t7J6I5W2M0opB2XNEKJrhyDsqWmwlW9nlvU8J+uSsMBc1Fx0ReG6nueWU6Q+B9tFA/jdNN9C3/92wetcP5R1gVh/tf2ZHl491WtAKomu3jYRzi7L+7KieulvaQOTXYba5i3b47Pr9phsYxIwH1L4Gfr+q4nX2B6UtYAobC6yej9IIC3qaJZbs13vp1RWLWdO1FPrrHbd+yxOEVUia7xFS2hQ+i1n/9uFY4MJqQRG1kKmV+csUdJg5EVXvZzaz4eKrDDbYB5bOn77WQDzcb/6n9+AaVT9nYLX2RaUdQVzYXNSldRBtaQ6DZaEx96tQib5set6KXC+rHCW9RyJde/z0Nv0tHSLlnfo+99L7H/bUNaVTIUtkTRVZ5NJC6qlze1fiyrLUMuNWS6RFc6ynttlj5ys3k267V0f7tD3v7/wGtuFsp5AvKQOmKZ2JbKmyqeGD5ZE10GVt8LaLpxcupsStST1leOz81xEFVHfAQD6/o8KX2ebUNYziFHW3tTLm6TcDeY/Xi/CAnlZ9UUGNrJaSUtvRVqSAqewgurlnKg9+v5PC19j21DWM4kDJ6SxRCb7IwWmP96bRNmdKu/dZdFOOg3Wgmpp19ZZvXkOL5LCOTY7Mumb6Pu/KNg/ASjrRYhpsb5SRLd22hE6IqoWdmeWpbzUYZeiKzCXVYu4NL55bV0114ikZfVEvWc0PQHKekFiWix/J/ka0796sAKLmDdmm66DisQtpuLqFLhE1rVpMJCPqqn6qT05yTE/HaZ3jKYnQlkvTFpYHOa3mNbdRNSdmQBfVL1uI6snakpaOMvePMVS3VSOV1+P+nX0/d8s7JekaEbvH3ZThZumvDA51GVfIvwBFhD/qkP+X0f+wU7/E4CV1d5exrs3VErW0q4buwykZU31GduUF5jeiuUvE/sjljH8ae4MyvoB6I4ds/I3kyKtJ6xIC8xlzdVdgfNl1fMUqX5f29ILhLrpXy3sj1go60dAkLZFlDYXZYF5apwTFphH21wqLHjCWlKNSTqayiD8BwDfAAD0/T8ufiZkTkpWDuQnpBIYWT8HQoTdIdRlbYTVkdXWY3MRFpi3Epd22wDroqrXJSP9pv9U+jGQBEyDP1K67hNEaeUfAfSfO3uNT56wgJ8G51qDgbSs3ogkGeSgB+A/oO///pRDJwko60dObDl+jfgXlDbaWmm1sIA/DDHXwATkI6oeywxMG5Du0Pf/fOLRkhyUtSJi6/FrTFuOc9IC6RFOOVktXqorrbsP6Pt/Ofv4SB7KWjFB3lv44up+WZsaL7UEC3Z4oKS4Pfr+3y5/QCQLZb0ygsCSMksdV4Yu5qIrMK+P7hH6RP/jg7x3koeyElIJ7GclpHIoKyGVQFkJqQTKSkglUFZCKoGyElIJlJWQSqCshFQCZSWkEigrIZVAWQmpBMpKSCVQVkIqgbISUgmUlZBKoKyEVAJlJaQSKCshlUBZCakEykpIJVBWQiqBshJSCZSVkEqgrIRUAmUlpBIoKyGVQFkJqQTKSkglUFZCKoGyElIJlJWQSqCshFQCZSWkEigrIZVAWQmpBMpKSCVQVkIqgbISUgmUlZBKoKyEVAJlJaQSKCshlUBZCakEykpIJVBWQiqBshJSCTcry38NwH++jzdCCAEAfFfqgWYcxw/5RgghJ8I0mJBKoKyEVAJlJaQSKCshlUBZCakEykpIJVBWQiqBshJSCZSVkEr4f72XyJ3dTGnqAAAAAElFTkSuQmCC\n",
      "text/plain": [
       "<Figure size 432x288 with 1 Axes>"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "w, h = 200, 200\n",
    "\n",
    "img = run(w, h)\n",
    "plt.imshow(img);\n",
    "plt.xticks([]); plt.yticks([]);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "1.95 ms ± 146 µs per loop (mean ± std. dev. of 7 runs, 100 loops each)\n"
     ]
    }
   ],
   "source": [
    "%timeit run(w, h)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
